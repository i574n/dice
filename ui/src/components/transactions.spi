open rust_operators


inl render () =
    print_static "<transactions.render>"
    leptos.log $'"transactions.render ()"'

    inl global_state : state.global_state = leptos.use_context () |> optionm'.unwrap

    inl accounts = leptos.create_memo fun () =>
        inl accounts =
            leptos.signal_get_untracked global_state.core_state .data
            |> leptos.signal_get_untracked
            |> fun data => data.accounts
            |> am'.from_vec
            |> am'.map_vec from_pair
            |> am'.filter_vec fst
            |> am'.map_vec snd
            |> fun x => x : _ i32 _

        accounts
        |> am'.map_vec fun account =>
            inl account = account |> sm'.from_std_string
            account, $'"https://api2.nearblocks.io/v1/account/" + !account + "/txns?&order=desc&page=1&per_page=25"'
        |> fun x =>
            inl x_log = x |> sm'.format_debug
            leptos.log $'"transactions.render () / url create_memo / result: " + string !x_log + ""'
            x

    inl urls = leptos.create_memo fun () =>
        accounts
        |> leptos.memo_get
        |> am'.map_vec snd

    inl root : rust.func0 (a _ (resultm.result' (optionm'.option' model.near.nearblocks.root) rust.std_string)) =
        state_core.use_requests urls model.near.nearblocks.root_unbox

    inl txns : leptos.memo (resultm.result' (optionm'.option' (array_base (string * unativeint * heap model.near.nearblocks.txn))) rust.std_string) =
        leptos.create_memo fun () =>
            inl accounts =
                accounts
                |> leptos.memo_get

            root
            |> rust.func0_get
            |> am'.map_vec resultm.unbox
            |> am'.mapi_vec fun i x =>
                inl account = index accounts i
                x
                |> resultm.map fun result =>
                    result
                    |> optionm'.unbox
                    |> optionm.map fun result =>
                        (a result.txns : _ i32 _)
                        |> am'.enumerate
                        |> am'.map_vec fun i', x' => account |> fst, i', heap x'
                        |> fun (a x : _ i32 _) => x
            |> fun x =>
                inl x_log = x |> sm'.format_debug
                leptos.log $'"transactions.render () / txns move / result length: " + (!x_log |> string |> String.length |> string) + ""'
                x
                |> am'.map_vec (resultm.map optionm'.box)
                |> am'.map_vec resultm.box
                |> fun x =>
                    ((;[] |> am'.to_vec |> Ok), x)
                    ||> am.fold fun acc x =>
                        x
                        |> resultm.unbox
                        |> resultm.map optionm'.unbox
                        |> fun x =>
                            match acc, x with
                            | Ok acc, Ok (Some x) => acc |> am'.vec_extend (x |> am'.to_vec) |> Ok
                            | _, Error error => error |> Error
                            | _ => acc
                |> resultm.map (am'.vec_sort_by_key fun _, _, x => x.block_timestamp)
                |> resultm.map (am'.vec_reverse >> am'.from_vec)
                |> resultm.map fun (a x : _ i32 _) => x |> Some |> optionm'.box
                |> resultm.box
    
    inl (settings_visible, set_settings_visible) = leptos.create_signal false

    inl settings_button =
        inl on_click () =
            set_settings_visible
            |> leptos.signal_update not
        inl on_click = join on_click
            
        inl class () =
            if settings_visible |> leptos.signal_get
            then ##""
            else ##" bg-gray-100"

        !\($'"let on_click = !on_click"')
        leptos.icon_button
            (leptos.settings_svg (Some $'"h-5 w-5"') [])
            class
            [
                $'"on:click=move |_| on_click()"'
                $'"aria-label=\\\"Settings\\\""'
            ]
        |> leptos.element_to_view

    ;[
        leptos.accordion "Transactions" true (Some settings_button) fun () =>
            :>(;[
                if settings_visible |> leptos.signal_get
                then
                    leptos.div [
                        $'"class=\\\"px-[12px] py-[10px]\\\""'
                    ] fun () =>
                        :>(;[
                            leptos.divider fun () =>
                                "Settings" |> leptos.text_fragment
                            |> leptos.element_to_view

                            leptos.grid_pair
                                { padding = None; cols = None; class = $'"items-center"' }
                                fun () => "Table View" |> leptos.text_fragment
                                fun () =>
                                    leptos.toggle
                                        "table-view"
                                        fun () =>
                                            leptos.signal_get_untracked global_state.core_state .data
                                            |> leptos.signal_get
                                            |> fun data => data.transactions_view_type = state.Table
                                        fun _ =>
                                            leptos.signal_get_untracked global_state.core_state .data
                                            |> leptos.signal_update fun data =>
                                                inl data = !data
                                                heap {
                                                    data with
                                                        transactions_view_type =
                                                            if data.transactions_view_type = state.Table
                                                            then state.Details
                                                            else state.Table
                                                }
                                        |> leptos.element_to_fragment
                            |> leptos.element_to_view
                        ])
                    |> leptos.element_to_view
                else ;[] |> leptos.views_to_view

                txns
                |> leptos.memo_get
                |> resultm.unbox
                |> resultm.map optionm'.unbox
                |> function
                    | Ok (Some transactions) =>
                        match
                            leptos.signal_get_untracked global_state.core_state .data
                            |> leptos.signal_get
                            |> fun data => data.transactions_view_type = state.Details
                        with
                        | true =>
                            leptos.div [
                                $'"class=\\\"grid flex-1 py-[10px] px-[12px] [gap:15px] sm:[grid-template-columns:repeat(auto-fill,minmax(500px,1fr))]\\\""'
                            ] fun () =>
                                a transactions
                                |> am'.map_vec fun account, index', transaction' =>
                                    !transaction'
                                    |> transaction.render account (index' |> i64)
                                    |> leptos.element_to_view
                                |> fun (a x : _ i32 _) => x
                                |> leptos.view_array_to_fragment
                            |> leptos.element_to_view

                        | false =>
                            leptos.table [
                                $'"class=\\\"flex-1 min-w-full divide-y-2 divide-gray-200 text-sm dark:divide-gray-700\\\""'
                            ] fun () =>
                                :>(;[
                                    leptos.thead [
                                        $'"class=\\\"ltr:text-left rtl:text-right\\\""'
                                    ] fun () =>
                                        :>(;[
                                            leptos.tr [

                                            ] fun () =>
                                                :>(;[
                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Account" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Block Timestamp" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Predecessor" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Receiver" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Action" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Action Method" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Deposit" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Fee" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Block Height" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        (a transactions : _ i32 _)
                                                        |> am.exists fun account, _index, transaction' =>
                                                            (a transaction'.logs : _ i32 _)
                                                            |> am.exists fun log =>
                                                                (log |> sm'.from_std_string |> sm.length) > 0i32
                                                        |> function
                                                            | false => ""
                                                            | _ => "Logs"
                                                            |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Outcome Status" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Hash" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Block Hash" |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.th [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                                    ] fun () =>
                                                        "Receipt ID" |> leptos.text_fragment
                                                    |> leptos.element_to_view
                                                ])
                                        ])
                                    |> leptos.element_to_view

                                    leptos.tbody [
                                        $'"class=\\\"divide-y divide-gray-200 dark:divide-gray-700\\\""'
                                    ] fun () =>
                                        a transactions
                                        |> am'.map_vec fun account, index', transaction' =>
                                            leptos.tr [
                                                $'"class=\\\"odd:bg-gray-50 dark:odd:bg-gray-800/50\\\""'
                                            ] fun () =>
                                                :>(;[
                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        account
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.block_timestamp
                                                        |> transaction.format_block_timestamp
                                                        |> to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.predecessor_account_id
                                                        |> to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.receiver_account_id
                                                        |> to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    a transaction'.actions
                                                    |> am'.map_vec fun { action method } =>
                                                        :>(;[
                                                            leptos.td [
                                                                $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                            ] fun () =>
                                                                match action |> sm'.from_std_string with
                                                                | "FUNCTION_CALL" => "Function Call"
                                                                | "DEPLOY_CONTRACT" => "Contract Deploy"
                                                                | "TRANSFER" => "Transfer"
                                                                | action => action
                                                                |> leptos.text_fragment
                                                            |> leptos.element_to_view

                                                            leptos.td [
                                                                $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                            ] fun () =>
                                                                method
                                                                |> optionm'.unbox
                                                                |> optionm.map to_string
                                                                |> optionm'.default_value "None"
                                                                |> leptos.text_fragment
                                                            |> leptos.element_to_view
                                                        ])
                                                    |> am'.map_vec leptos.fragment_to_view
                                                    |> fun (a x : _ i32 leptos.view) => :>x
                                                    |> leptos.fragment_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.actions_agg.deposit
                                                        |> sm'.obj_to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.outcomes_agg.transaction_fee
                                                        |> sm'.obj_to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.block.block_height
                                                        |> sm'.obj_to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500 [overflow:auto]\\\""'
                                                    ] fun () =>
                                                        match transaction'.logs with
                                                        | ;[] => "" |> leptos.text_fragment
                                                        | _ =>
                                                            (a transaction'.logs : _ i32 _)
                                                            |> am'.map_vec sm'.from_std_string
                                                            |> sm'.concat "\n"
                                                            |> sm'.replace "\\n" "\n"
                                                            |> leptos.text_block_pre "[max-height:20vh] [width:100vw] [max-width:100vw]"
                                                            |> leptos.element_to_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.outcomes.status
                                                        |> sm'.obj_to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                        transaction'.transaction_hash
                                                        |> to_string
                                                        |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                            transaction'.included_in_block_hash
                                                            |> to_string
                                                            |> leptos.text_fragment
                                                    |> leptos.element_to_view

                                                    leptos.td [
                                                        $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                    ] fun () =>
                                                            transaction'.receipt_id
                                                            |> to_string
                                                            |> leptos.text_fragment
                                                    |> leptos.element_to_view
                                                ])
                                            |> leptos.element_to_view
                                        |> fun (a x : _ i32 _) => x
                                        |> leptos.view_array_to_fragment
                                    |> leptos.element_to_view
                                ])
                            |> leptos.element_to_view
                    | Error error =>
                        leptos.div [
                            $'"class=\\\"flex flex-1 items-center [gap:4px] [padding:5px]\\\""'
                        ] fun () => :>(;[
                            leptos.span [
                                $'"class=\\\"[height:20px]\\\""'
                            ] fun () => :>(;[
                                leptos.x_red_svg () |> leptos.element_to_view
                            ])
                            |> leptos.element_to_view

                            leptos.pre [
                                $'"class=\\\"[overflow-y:auto]\\\""'
                            ] fun () =>
                                error |> sm'.from_std_string |> leptos.text_fragment
                            |> leptos.element_to_view
                        ])
                        |> leptos.element_to_view
                    | _ =>
                        leptos.div [
                            $'"class=\\\"grid place-content-center py-[10vh]\\\""'
                        ] fun () =>
                            leptos.div [
                                $'"class=\\\"flex flex-1 [gap:4px] items-center\\\""'
                            ] fun () => :>(;[
                                leptos.loading_svg () |> leptos.element_to_view
                                $'"Loading..."' |> leptos.text_view
                            ])
                            |> leptos.element_to_fragment
                        |> leptos.element_to_view
            ])
        |> leptos.element_to_view

        if leptos.signal_get_untracked global_state.core_state .debug |> leptos.signal_get then
            leptos.accordion "Debug" true None fun () =>
                :>(;[
                    leptos.grid_pair
                        { padding = None; cols = None; class = "" }
                        fun () => "Transactions" |> leptos.text_fragment
                        fun () =>
                            txns
                            |> leptos.memo_get
                            |> sm'.format_pretty'
                            |> sm'.from_std_string
                            |> leptos.text_block_pre "[max-height:70vh]"
                            |> leptos.element_to_fragment
                ])
            |> leptos.element_to_view
        else ;[] |> leptos.views_to_view
    ]
    |> leptos.view_array_to_fragment
