open rust_operators
open leptos

inl render () =
    print_static "<dice.render>"
    leptos.log $'"dice.render (1)"'

    inl global_state : state.global_state = leptos.use_context () |> optionm'.unwrap

    inl { accounts_txns_requests txns rpc_responses } = hooks.use_transactions.render global_state

    inl roll_action = leptos.create_action fun (value : heap i32) =>
        inl value_log = value |> sm'.format_debug
        leptos.log $'"dice.render (2) / set_max_action / value: " + string !value_log + ""'
        fun () =>
            inl url = "https://rpc.mainnet.near.org"
            inl body =
                {
                    jsonrpc = "2.0" |> sm'.to_std_string
                    id = "dontcare" |> sm'.to_std_string
                    method = "EXPERIMENTAL_tx_status" |> sm'.to_std_string
                    params = ;[
                        "9JgptZpFQGtUvusnoF9ysVqHXD8fXCri8mFxPTMnt3kS" |> sm'.to_std_string
                        "bowen" |> sm'.to_std_string
                    ]
                }
                |> model.near.rpc.rpc_input_box

            inl result : _ _ sm'.std_string =
                url
                |> networking.new_request_post
                |> networking.request_json body
                |> networking.request_send
                |> async.await
                |> resultm.map_error' sm'.format'
                |> resultm.try'
                |> networking.response_text
                |> async.await
                |> resultm.map_error' sm'.format'
                |> resultm.try'
                |> Some
                |> Ok
                |> resultm.box
                |> leptos.log_format fun log => $'"dice_view.render (x) / roll_action create_action / result1 / result: " + string !log + ""'

            match result |> resultm.unbox with
            | Ok (Some value) =>
                value
                |> sm'.from_std_string
                |> sm'.deserialize
                |> resultm.unbox
                |> resultm.map model.near.rpc.rpc_response_unbox
                |> resultm.map Some
            | _ => Ok None
            |> resultm.box
            |> leptos.log_format fun log => $'"dice_view.render (x) / roll_action create_action / result2 / result: " + string !log + ""'
        |> async.future_init (4, 3) 1

    inl roll = leptos.create_memo fun () =>
        roll_action
        |> leptos.action_value
        |> leptos.signal_get
        |> optionm'.map resultm.unbox
        |> optionm'.unbox
        |> resultm.flatten_option
        |> resultm.map optionm'.box
        |> resultm.box
        |> leptos.log_format fun log => $'"dice_view.render (x) / roll create_memo / result: " + string !log + ""'

    inl error_message = leptos.create_memo fun () =>
        match roll |> leptos.memo_get |> resultm.unbox |> resultm.map optionm'.unbox with
        | Ok (Some { error }) => error |> model.near.rpc.format_error
        | _ => None
        |> optionm'.box
        |> leptos.log_format fun log => $'"dice_view.render (x) / error_message create_memo / result: " + string !log + ""'

    inl account =
        leptos.create_memo fun () =>
            leptos.signal_get_untracked global_state.core_state .data
            |> leptos.signal_get
            |> fun data =>
                data.account
                |> optionm'.unbox
                |> optionm.map sm'.from_std_string

    inl accounts =
        leptos.create_memo fun () =>
            leptos.signal_get_untracked global_state.core_state .data
            |> leptos.signal_get
            |> fun data =>
                data.accounts
                |> am'.from_vec
                |> fun x => x : _ i32 _
                |> am'.map_vec from_pair
                |> am'.filter_vec fst
                |> am'.map_vec snd
                |> am'.mapi_vec fun (i : i32) account =>
                    new_pair
                        i
                        (leptos.create_signal account)

    inl accounts_data =
        leptos.create_memo fun () =>
            accounts
            |> leptos.memo_get
            |> am'.map_vec from_pair
            |> am'.map_vec fun i, x =>
                i, x |> fst
            |> fun (a x) => x
            |> am'.to_vec

    ;[
        ui.accordion.render
            fun props => { props with
                title = "Roll"
                summary_class = fun () => "py-[7px]"
            }
            fun () => :>(;[
                leptos.div [
                    $'"class=\\\"flex flex-col p-[10px] gap-[8px]\\\""'
                ] fun () =>
                    :>(;[
                        leptos.div [
                            $'"class=\\\"flex [flex-direction:column] [align-items:flex-start]\\\""'
                        ] fun () =>
                            :>(;[
                                "Account" |> leptos.text_view

                                ui.dropdown.render fun props => { props with
                                    data = accounts_data |> Some |> optionm'.box
                                    item = account |> Some |> optionm'.box
                                    select = fun (i, value) =>
                                        inl data =
                                            leptos.signal_get_untracked global_state.core_state .data
                                            |> leptos.signal_get_untracked
                                            |> fun data => !data

                                        leptos.signal_get_untracked global_state.core_state .data
                                        |> leptos.signal_set (heap { data with
                                            account = value |> sm'.to_std_string |> Some |> optionm'.box
                                        })
                                }
                                |> leptos.element_to_view
                            ])
                        |> leptos.element_to_view

                        leptos.div [
                            $'"class=\\\"flex [flex-direction:column] [align-items:flex-start]\\\""'
                        ] fun () =>
                            :>(;[
                                "Max" |> leptos.text_view

                                ui.text_input.render
                                    fun () => ""
                                    [
                                    ]
                                    fun () =>
                                        leptos.signal_get_untracked global_state.core_state .data
                                        |> leptos.signal_get_untracked
                                        |> fun x => x.max

                                    fun value =>
                                        inl data =
                                            leptos.signal_get_untracked global_state.core_state .data
                                            |> leptos.signal_get_untracked
                                            |> fun data => !data

                                        leptos.signal_get_untracked global_state.core_state .data
                                        |> leptos.signal_set (heap { data with
                                            max =
                                                match $"!value |> string" with
                                                | "" => 1
                                                | x => $"!x |> int32"
                                        })
                                |> leptos.element_to_view
                            ])
                        |> leptos.element_to_view

                        leptos.div [
                            $'"class=\\\"flex\\\""'
                        ] fun () =>
                            :>(;[
                                ui.loading_button.render
                                    "Roll"
                                    fun () =>
                                        roll_action
                                        |> leptos.action_pending
                                        |> leptos.signal_get
                                    fun () =>
                                        inl max =
                                            leptos.signal_get_untracked global_state.core_state .data
                                            |> leptos.signal_get_untracked
                                            |> fun x => heap x.max

                                        roll_action |> leptos.action_dispatch max
                                |> leptos.element_to_view

                                match error_message |> leptos.memo_get |> optionm'.unbox with
                                | Some error_message =>
                                    error_message
                                    |> ui.error_label.render
                                    |> leptos.element_to_view
                                | _ => ;[] |> leptos.views_to_view
                            ])
                        |> leptos.element_to_view
                ])
            ])
        |> leptos.element_to_view

        ui.accordion.render
            fun props => { props with
                title = "History"
                summary_class = fun () => "py-[7px]"
            }
            fun () => :>(;[
                match accounts_txns_requests |> leptos.memo_get with
                | a ;[] =>
                    "No account selected"
                    |> ui.error_label.render
                    |> leptos.element_to_view
                | _ =>
                    txns
                    |> leptos.memo_get
                    |> resultm.unbox
                    |> resultm.map optionm'.unbox
                    |> function
                        | Ok (Some transactions) =>
                            inl transactions =
                                a transactions
                                |> am'.filter_vec function
                                    | { account txn } =>
                                        inl is_call =
                                            a txn.actions
                                            |> am'.map_vec fun transaction =>
                                                {
                                                    action =
                                                        transaction.action
                                                        |> sm'.from_std_string
                                                    method =
                                                        transaction.method
                                                        |> optionm'.unbox
                                                        |> optionm.map sm'.from_std_string
                                                }
                                            |> am'.filter_vec fun transaction =>
                                                {
                                                    action = "FUNCTION_CALL"
                                                    method = Some "generate_random_number"
                                                } = transaction
                                            |> fun (x : _ i32 _) => length x > 0
                                        inl is_receiver =
                                            account = (txn.receiver_account_id |> sm'.from_std_string)
                                        is_call && is_receiver
                                    | _ => false
                                |> fun (a x : _ i32 _) => x

                            ui.table.render
                                (
                                    leptos.tr [

                                    ] fun () =>
                                        :>(;[
                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Block Timestamp" |> leptos.text_fragment
                                            |> leptos.element_to_view

                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Predecessor" |> leptos.text_fragment
                                            |> leptos.element_to_view

                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Receiver" |> leptos.text_fragment
                                            |> leptos.element_to_view

                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Fee" |> leptos.text_fragment
                                            |> leptos.element_to_view

                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Result" |> leptos.text_fragment
                                            |> leptos.element_to_view

                                            leptos.th [
                                                $'"class=\\\"whitespace-nowrap px-4 py-2 font-medium text-gray-900 dark:text-white\\\""'
                                            ] fun () =>
                                                "Status" |> leptos.text_fragment
                                            |> leptos.element_to_view
                                        ])
                                    |> leptos.element_to_fragment
                                )
                                (
                                    inl rpc_responses =
                                        rpc_responses
                                        |> leptos.memo_get
                                    a transactions
                                    |> am'.map_vec fun { txn i } =>
                                        inl rpc_response = index rpc_responses (i |> i32)




                                        leptos.tr [
                                            $'"class=\\\"odd:bg-gray-50 dark:odd:bg-gray-800/50\\\""'
                                        ] fun () =>
                                            :>(;[
                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    txn.block_timestamp
                                                    |> date_time.format_timestamp
                                                    |> to_string
                                                    |> leptos.text_fragment
                                                |> leptos.element_to_view

                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    txn.predecessor_account_id
                                                    |> to_string
                                                    |> leptos.text_fragment
                                                |> leptos.element_to_view

                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    txn.receiver_account_id
                                                    |> to_string
                                                    |> leptos.text_fragment
                                                |> leptos.element_to_view

                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    txn.outcomes_agg.transaction_fee
                                                    |> sm'.obj_to_string
                                                    |> leptos.text_fragment
                                                |> leptos.element_to_view

                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    match txn.logs with
                                                    | ;[] => "" |> leptos.text_fragment
                                                    | _ =>
                                                        (a txn.logs : _ i32 _)
                                                        |> am'.map_vec sm'.from_std_string
                                                        |> fun x =>
                                                            (x, None)
                                                            ||> am.foldBack fun x acc =>
                                                                inl logs = x |> sm'.split " / result: "
                                                                match acc, logs with
                                                                | None, ;[ _; result ] => Some result
                                                                | _ => acc
                                                        |> optionm'.default_value "?"
                                                        |> leptos.text_fragment
                                                |> leptos.element_to_view

                                                leptos.td [
                                                    $'"class=\\\"whitespace-nowrap px-4 py-2 text-gray-700 dark:text-gray-500\\\""'
                                                ] fun () =>
                                                    if txn.outcomes.status
                                                    then "Ok"
                                                    else "Outcome Error"
                                                    |> fun s =>
                                                        inl rpc_response_log =
                                                            rpc_response
                                                            |> resultm.unbox
                                                            |> resultm.map optionm'.unbox
                                                            |> function
                                                                | Ok (Some { error }) =>
                                                                    error
                                                                    |> model.near.rpc.format_error
                                                                    |> optionm'.default_with sm'.format_ellipsis'
                                                                | Error error =>
                                                                    error
                                                                    |> sm'.from_std_string
                                                                | rpc_response =>
                                                                    inl rpc_response_log =
                                                                        rpc_response
                                                                        |> sm'.format_ellipsis'
                                                                    $'$"RPC Response: {!rpc_response_log}"'
                                                        $'$"{!s} / {!rpc_response_log}"'
                                                    |> leptos.text_fragment
                                                |> leptos.element_to_view
                                            ])
                                        |> leptos.element_to_view
                                    |> fun (a x : _ i32 _) => x
                                    |> leptos.view_array_to_fragment
                                )
                                |> leptos.element_to_view
                        | Error error =>
                            error
                            |> sm'.from_std_string
                            |> ui.error_label.render
                            |> leptos.element_to_view
                        | _ =>
                            ui.loading_panel.render ()
                            |> leptos.element_to_view
            ])
        |> leptos.element_to_view
    ]
    |> leptos.view_array_to_fragment
