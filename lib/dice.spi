/// # dice (Dice)

/// ## sixth_power_sequence
inl sixth_power_sequence () =
    stream.iterate ((*) 6) 1

/// ## accumulate_dice_rolls
inl accumulate_dice_rolls
    (log : option (string -> ()))
    (rolls : list u8)
    (power : i8)
    acc
    =
    inl rec body acc power rolls =
        match rolls with
        | _ when power < 0 =>
            inl result = acc + 1
            log |> optionm'.iter ((|>) $'$"accumulate_dice_rolls / power: {!power} / acc: {!acc} / result: {!result}"')
            Some (result, rolls)
        | [] => None
        | roll :: rest when roll > 1 =>
            inl coeff = sixth_power_sequence () |> stream.item power
            inl value = conv (roll - 1) * coeff
            log |> optionm'.iter ((|>) $'$"accumulate_dice_rolls / power: {!power} / acc: {!acc} / roll: {!roll} / value: {!value}"'
            )
            loop (acc + value) (power - 1) rest
        | roll :: rest =>
            log |> optionm'.iter ((|>) $'$"accumulate_dice_rolls / power: {!power} / acc: {!acc} / roll: {!roll}"')
            loop acc (power - 1) rest
    and inl loop acc power rolls =
        if var_is rolls |> not
        then body acc power rolls
        else
            inl acc = dyn acc
            join body acc power rolls
    loop acc power rolls

/// ## roll_within_bounds
inl roll_within_bounds (log : option (string -> ())) max rolls =
    inl power = listm.length rolls - 1
    match accumulate_dice_rolls log rolls power 0 with
    | Some (result, _) when result >= 1 && result <= max => Some result
    | _ => None

/// ## calculate_dice_count
inl calculate_dice_count (log : option (string -> ())) max =
    inl rec body n p =
        inl return () =
            log |> optionm'.iter ((|>) $'$"calculate_dice_count / max: {!max} / n: {!n} / p: {!p}"')
            n

        if p < max then
            inl p' = p * 6
            if p' > p
            then loop (n + 1) p'
            else return ()
        else return ()

    and inl loop n p =
        if var_is max |> not
        then body n p
        else
            inl n = dyn n
            inl p = dyn p
            join body n p
    if max = 1
    then 1
    else loop 0 1

/// ## roll_dice
let roll_dice () : u8 =
    random' 1 7

/// ## rotate_number
inl rotate_number forall t {number}. (max : i64) (n : t) : t =
    (conv n - 1 + max) % max + 1 |> conv

/// ## rotate_numbers
inl rotate_numbers max items =
    items |> stream.map (rotate_number max)

/// ## create_sequential_roller
inl create_sequential_roller (log : option (string -> ())) list =
    log |> optionm'.iter ((|>) $'$"create_sequential_roller ()"')
    inl s = list |> listm.rev |> listm.append list |> stream.from_list |> stream.memoize
    inl current_index = mut 0i64
    inl acc = mut 1
    inl len = mut -1
    inl last_item = mut None
    let rec loop () =
        fun () =>
            inl current_index = *current_index
            inl acc = *acc
            inl len = *len
            inl last_item = *last_item
            log |> optionm'.iter ((|>) $'$"create_sequential_roller / roll / current_index: {!current_index} / acc: {!acc} / len: {!len} / last_item: %A{!last_item}"')
        |> fun x => x ()
        match s () |> stream.try_item *current_index with
        | Some item =>
            current_index <- *current_index + 1
            last_item <- Some item
            item
        | None =>
            log |> optionm'.iter ((|>) $'$"create_sequential_roller / roll / None"')
            if *len = -1
            then len <- *current_index
            acc <-
                if *acc >= *len
                then 1
                else *acc + 1
            current_index <- *acc - 1
            last_item <- None
            loop ()
    loop

/// ## roll_progressively
inl roll_progressively (log : option (string -> ())) roll reroll max =
    inl power = (calculate_dice_count log max) - 1
    let rec loop rolls size =
        if size < power + 1
        then loop (roll () :: rolls) (size + 1)
        else accumulate_dice_rolls log rolls power 0 |> function
            | Some (result, _) when result <= max => result
            | _ when reroll => loop (listm.init power (fun _ => roll ())) power
            | _ => loop (roll () :: rolls) (size + 1)
    loop [] 0

/// ## main
inl main (_args : array_base string) =
    inl result = roll_progressively (Some console.write_line) roll_dice true (limit.max : i64)
    console.write_line ($'$"result: {!result}"' : string)
    0i32

inl main () =
    inl rotate_numbers' : i64 -> stream.stream u8 -> stream.stream u8 =
        rotate_numbers

    inl create_sequential_roller' :
        optionm'.option' (string -> ())
        -> list u8 
        -> (() -> u8)
        =
        optionm'.unbox >> create_sequential_roller

    inl roll_progressively' :
        optionm'.option' (string -> ()) 
        -> (() -> u8)
        -> bool
        -> u64
        -> u64
        =
        optionm'.unbox >> roll_progressively

    inl roll_within_bounds' :
        optionm'.option' (string -> ())
        -> u64
        -> list u8
        -> optionm'.option' u64
        =
        fun a b c =>
            (optionm'.unbox >> roll_within_bounds) a b c
            |> optionm'.box

    $'let rotate_numbers x = !rotate_numbers' x' : ()
    $'let create_sequential_roller x = !create_sequential_roller' x' : ()
    $'let roll_progressively x = !roll_progressively' x' : ()
    $'let roll_within_bounds x = !roll_within_bounds' x' : ()
    $'let main args = !main args' : ()
