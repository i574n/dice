#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[{"name":"spiral"}]}}

#!markdown

# dice (Dice)

#!spiral

//// test
//// test_force

open testing

#!markdown

## sixth_power_sequence

#!spiral

inl sixth_power_sequence () =
    stream.iterate_map ((*) 6) (fun x => if x <= 0 then None else Some x) 1

#!spiral

//// test

sixth_power_sequence ()
|> stream.take_while (fun _ i => i <= 7i32)
|> stream.to_list
|> _assert_eq [ 1u64; 6; 36; 216; 1296; 7776; 46656; 279936 ]

#!markdown

## accumulate_dice_rolls

#!spiral

inl accumulate_dice_rolls
    (rolls : list u8)
    (power : i8)
    acc
    =
    // inl rec body acc power rolls =
    inl rec body forall a {number} b {number} c {number}.
        (acc : a)
        (power : b)
        (rolls : list c)
        : option (a * list c)
        =
        match rolls with
        | _ when power < 0 =>
            inl result = acc + 1
            trace Debug
                fun () => "dice.accumulate_dice_rolls"
                fun () => { power acc result }
            Some (result, rolls)
        | [] => None
        | roll :: rest when roll > 1 =>
            inl coeff = sixth_power_sequence () |> stream.item power
            inl value = conv (roll - 1) * coeff
            trace Debug
                fun () => "dice.accumulate_dice_rolls"
                fun () => { power acc roll value }
            루프 (acc + value) (power - 1) rest
        | roll :: rest =>
            trace Debug
                fun () => "dice.accumulate_dice_rolls"
                fun () => { power acc roll }
            루프 acc (power - 1) rest
    // and inl 루프 acc power rolls =
    and inl 루프 forall a {number} b {number} c {number}.
        (acc : a)
        (power : b)
        (rolls : list c)
        : option (a * list c)
        =
        if var_is rolls |> not
        then body acc power rolls
        else
            inl acc = dyn acc
            join body acc power rolls
    루프 acc power rolls

#!spiral

//// test

accumulate_dice_rolls [ 6; 5; 4; 3; 2 ] 0 1000i32
|> _assert_eq (Some (1006, [ 5; 4; 3; 2 ]))

#!spiral

//// test

accumulate_dice_rolls [ 6; 5; 4; 3; 2 ] 1 1000i32
|> _assert_eq (Some (1035, [ 4; 3; 2 ]))

#!spiral

//// test

accumulate_dice_rolls [ 6; 5; 4; 3; 2 ] 2 1000i32
|> _assert_eq (Some (1208, [ 3; 2 ]))

#!markdown

## roll_within_bounds

#!spiral

inl roll_within_bounds max rolls =
    inl power = listm.length rolls - 1
    match accumulate_dice_rolls rolls power 0 with
    | Some (result, _) when result >= 1 && result <= max => Some result
    | _ => None

#!spiral

//// test

roll_within_bounds 2000i32 [ 1; 5; 4; 4; 5 ]
|> _assert_eq (Some 995)

#!spiral

//// test

roll_within_bounds 2000i32 [ 2; 2; 6; 4; 5 ]
|> _assert_eq (Some 1715)

#!spiral

//// test

roll_within_bounds 2000i32 [ 4; 1; 1; 2; 3 ]
|> _assert_eq None

#!markdown

## calculate_dice_count

#!spiral

// inl calculate_dice_count max =
inl calculate_dice_count forall a {number} b {number}. (max : a) : b =
    // inl rec body n p =
    inl rec body forall t {number}. (n : t) (p : a) : t =
        inl return () =
            trace Debug
                fun () => "dice.calculate_dice_count"
                fun () => { max p n }
            n
        if p < max then
            inl p' = p * 6
            if p' > p
            then 루프 (n + 1) p'
            else return ()
        else return ()
    // and inl 루프 n p =
    and inl 루프 forall t {number}. (n : t) (p : a) : t =
        if var_is max |> not
        then body n p
        else
            inl n = dyn n
            inl p = dyn p
            join body n p
    if max = 1
    then 1
    else 루프 0 1

#!spiral

//// test

calculate_dice_count 36i32
|> _assert_eq 2i32

#!spiral

//// test

calculate_dice_count 7777i32
|> _assert_eq 6i32

#!markdown

## roll_dice

#!spiral

let roll_dice () : u8 =
    random' 1 7

#!markdown

## rotate_number

#!spiral

inl rotate_number forall t {number}. (max : i64) (n : t) : t =
    (conv n - 1 + max) % max + 1 |> conv

#!markdown

## rotate_numbers

#!spiral

inl rotate_numbers max items =
    items |> stream.map (rotate_number max)

#!spiral

//// test

listm'.init_series -1 14 1i32
|> stream.from_list
|> rotate_numbers 6
|> stream.to_list
|> _assert_eq [ 5; 6; 1; 2; 3; 4; 5; 6; 1; 2; 3; 4; 5; 6; 1; 2 ]

#!markdown

## create_sequential_roller

#!spiral

inl create_sequential_roller list =
    inl s = list |> listm.rev |> listm.append list |> stream.from_list |> stream.memoize
    inl current_index = mut 0i64
    inl acc = mut 1
    inl len = mut -1
    inl last_item = mut None
    let rec 루프 () =
        fun () =>
            inl current_index = *current_index
            inl acc = *acc
            inl len = *len
            inl last_item = *last_item |> optionm'.box
            trace Debug
                fun () => "dice.create_sequential_roller / roll"
                fun () => { current_index acc len last_item = last_item |> sm'.format_debug }
        |> fun x => x ()
        match s () |> stream.try_item *current_index with
        | Some item =>
            current_index <- *current_index + 1
            last_item <- Some item
            item
        | None =>
            trace Debug (fun () => "dice.create_sequential_roller / roll / None") id
            if *len = -1
            then len <- *current_index
            acc <-
                if *acc >= *len
                then 1
                else *acc + 1
            current_index <- *acc - 1
            last_item <- None
            루프 ()
    루프

#!spiral

//// test

inl sequential_roll = create_sequential_roller [ 1i32; 2; 3; 4 ]

am.init 50i32 (ignore >> sequential_roll)
|> _assert_eq (a ;[ 1; 2; 3; 4; 4; 3; 2; 1; 2; 3; 4; 4; 3; 2; 1; 3; 4; 4; 3; 2; 1; 4; 4; 3; 2; 1;
4; 3; 2; 1; 3; 2; 1; 2; 1; 1; 1; 2; 3; 4; 4; 3; 2; 1; 2; 3; 4; 4; 3; 2 ] : a i32 i32)

#!markdown

## roll_progressively

#!spiral

inl roll_progressively roll reroll max =
    inl power = (calculate_dice_count max) - 1
    let rec 루프 rolls size =
        if size < power + 1
        then 루프 (roll () :: rolls) (size + 1)
        else accumulate_dice_rolls rolls power 0 |> function
            | Some (result, _) when result <= max => result
            | _ when reroll => 루프 (listm.init power (fun _ => roll ())) power
            | _ => 루프 (roll () :: rolls) (size + 1)
    루프 [] 0

#!spiral

//// test

roll_progressively roll_dice false 1
|> _assert_eq 1i32

#!spiral

//// test

inl sequential_roll = create_sequential_roller [ 5; 4; 4; 5; 1 ]

roll_progressively sequential_roll false 2000i32
|> _assert_eq 995

#!spiral

//// test

inl sequential_roll = create_sequential_roller [ 5; 4; 4; 5; 2 ]

roll_progressively sequential_roll false 2000i32
|> _assert_eq 1678

#!spiral

//// test

inl { level } = get_trace_state_or_init None
level <- Info

join dyn listm'.init_series 1i32 10000 1
|> listm.map (fun _ => roll_progressively roll_dice false 10i32)
|> listm'.group_by id
|> listm.map (fun (k, v) => k, (listm.length v : i32))
|> listm'.box
|> listm'.to_array'
|> fun ar =>
    $'!ar |> Array.sortBy (fun (struct (a, b)) -> a)' : a i32 $'struct (int32 * int32)'

#!spiral

//// test

inl { level } = get_trace_state_or_init None
level <- Info

join dyn listm'.init_series 1i32 10000 1
|> listm.map (fun _ => roll_progressively roll_dice true 10i32)
|> listm'.group_by id
|> listm.map (fun (k, v) => k, (listm.length v : i32))
|> listm'.box
|> listm'.to_array'
|> fun ar =>
    $'!ar |> Array.sortBy (fun (struct (a, b)) -> a)' : a i32 $'struct (int32 * int32)'

#!spiral

//// test
//// timeout=300000

inl { level } = get_trace_state_or_init None
level <- Info

join dyn listm'.init_series 1i32 100 1
|> listm.iter fun n =>
    listm'.init_series 0i32 1 1
    |> listm.iter fun reroll =>
        join dyn listm'.init_series 1i32 3500 1
        |> listm.map fun _ => roll_progressively roll_dice (reroll = 1) n
        |> listm'.group_by id
        |> listm.length
        |> __assert_eq Silent n

#!markdown

## main

#!spiral

inl main (_args : array_base string) =
    inl result = roll_progressively roll_dice true (limit.max : i64)
    trace Debug
        fun () => "dice.main"
        fun () => { result }
    0i32

inl main () =
    inl rotate_numbers' : i64 -> stream.stream u8 -> stream.stream u8 =
        rotate_numbers

    inl create_sequential_roller' :
        list u8
        -> (() -> u8)
        =
        create_sequential_roller

    inl roll_progressively' :
        (() -> u8)
        -> bool
        -> u64
        -> u64
        =
        roll_progressively

    inl roll_within_bounds' :
        u64
        -> list u8
        -> optionm'.option' u64
        =
        fun a b =>
            roll_within_bounds a b
            |> optionm'.box

    $'let rotate_numbers x = !rotate_numbers' x' : ()
    $'let create_sequential_roller x = !create_sequential_roller' x' : ()
    $'let roll_progressively x = !roll_progressively' x' : ()
    $'let roll_within_bounds x = !roll_within_bounds' x' : ()
    $'let main args = !main args' : ()
